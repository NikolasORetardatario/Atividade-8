programa 
{


//Trabalho feito por:
//Graciely Azevedo
//Maria Eduarda Glasser
    
    
    inclua biblioteca Util --> u
    
	funcao inicio() {
	    
		inteiro tipoAposta 
		inteiro vezesJogadas
		
		escreva(" Selecione um jogo \n")
		escreva("1 Mega-sena                    \n")
		escreva("2 Quina.                       \n")
		escreva("3 Loto-mania                   \n")
		escreva("4 Loto-fácil                   \n")
		escreva("...............................\n")
		escreva("Digite o número correspondente ao seu jogo : ")
		leia(tipoAposta)
		escreva("Esreceve quantas vezes quer jogar:")
		leia(vezesJogadas)


		inteiro quantidadeDezenas
		inteiro numerosGerados[50]

		
		escolha(tipoAposta){
		    
		    caso 1: 
		
    		escreva("Quantas dezenas vc quer apostar? \n")
    		leia(quantidadeDezenas)
    		
    		enquanto(quantidadeDezenas < 6 ou quantidadeDezenas > 15){
    		    escreva("Na Mega-sena só pode apostar de 6 á 15 dezenas, tente novamente:")
    		    leia(quantidadeDezenas)
    		}
    		
    		limpa()
    		
    		para(inteiro c = 0; c< vezesJogadas; c++){
                gerarNumeros(numerosGerados, quantidadeDezenas, 1, 60)
        		ordenar(numerosGerados, quantidadeDezenas)
        		para(inteiro i = 0; i< quantidadeDezenas; i++){
        		    escreva(numerosGerados[i], " ")
    	    	}
    	    	escreva("\n\n")
    		}
    		
    		real valorDezenas[16]
		    
		    valorDezenas[6]  = 4.50
		    valorDezenas[7]  = 31.50
		    valorDezenas[8]  = 126.00
		    valorDezenas[9]  = 378.00
		    valorDezenas[10] = 945.00
		    valorDezenas[11] = 2.079
		    valorDezenas[12] = 4.158
		    valorDezenas[13] = 7.722
		    valorDezenas[14] = 13513.50
		    valorDezenas[15] = 22520.50
    		    
    		gasto(valorDezenas, quantidadeDezenas, vezesJogadas)
    		
    		pare

    			
    		
    		caso 2:
		
    		escreva("Quantas dezenas vc quer apostar? \n")
    		leia(quantidadeDezenas)
    		
    		enquanto(quantidadeDezenas < 5 ou quantidadeDezenas > 15){
    		    escreva("Na Quina só pode apostar de 5 a 15 dezenas, tente novamente:")
    		    leia(quantidadeDezenas)
    		}
    		
    		limpa()
    		
    		para(inteiro c = 0; c< vezesJogadas; c++){
                gerarNumeros(numerosGerados, quantidadeDezenas, 1, 80)
        		ordenar(numerosGerados, quantidadeDezenas)
        		para(inteiro i = 0; i< quantidadeDezenas; i++){
        		    escreva(numerosGerados[i], " ")
    	    	}
    	    	escreva("\n\n")
    		}
    		
    		real valorDezenasQuina[16]
		    
		    valorDezenasQuina[5]  = 2.00
		    valorDezenasQuina[6]  = 12.00
		    valorDezenasQuina[7]  = 42.00
		    valorDezenasQuina[8]  = 112.00
		    valorDezenasQuina[9]  = 252.00
		    valorDezenasQuina[10] = 504.00
		    valorDezenasQuina[11] = 924.00
		    valorDezenasQuina[12] = 1584.00
		    valorDezenasQuina[13] = 2574.00
		    valorDezenasQuina[14] = 4004.00
		    valorDezenasQuina[15] = 6006.00
    		    
    		gasto(valorDezenasQuina, quantidadeDezenas, vezesJogadas)
    		
    		pare
    		
	
    		
    		caso 3: 
		
    		escreva("Na lotomania o número fixo de dezenas é 50 ")
    		quantidadeDezenas = 50
    		
    		limpa()
    		
    		para(inteiro c = 0; c< vezesJogadas; c++){
                gerarNumeros(numerosGerados, quantidadeDezenas, 1, 100)
        		ordenar(numerosGerados, quantidadeDezenas)
        		para(inteiro i = 0; i< quantidadeDezenas; i++){
        		    escreva(numerosGerados[i], " ")
    	    	}
    	    	escreva("\n\n")
    		}
    		
    		real valorLotomania = 2.50
		   
    	    escreva("No total você gastou R$", valorLotomania * vezesJogadas,"\n")
    		
    		pare
    		
	

    		caso 4:
		
    		escreva("Quantas dezenas você quer apostar? \n")
    		leia(quantidadeDezenas)
    		
    		enquanto(quantidadeDezenas < 15 ou quantidadeDezenas > 20){
    		    escreva("Na Loto-fácil só pode apostar de 15 a 20 dezenas, tente novamente:")
    		    leia(quantidadeDezenas)
    		}
    		
    		limpa()
    		
    		para(inteiro c = 0; c< vezesJogadas; c++){
                gerarNumeros(numerosGerados, quantidadeDezenas, 1, 25)
        		ordenar(numerosGerados, quantidadeDezenas)
        		para(inteiro i = 0; i< quantidadeDezenas; i++){
        		    escreva(numerosGerados[i], " ")
    	    	}
    	    	escreva("\n\n")
    		}
    		
    		real valorDezenasLotofacil[21]
		    
		    valorDezenasLotofacil[15]  = 2.50
		    valorDezenasLotofacil[16]  = 40.00
		    valorDezenasLotofacil[17]  = 2040.00
		    valorDezenasLotofacil[18]  = 2040.00
		    valorDezenasLotofacil[19]  = 9690.00
		    valorDezenasLotofacil[20]  = 38760.00
    		    
    		gasto(valorDezenasLotofacil, quantidadeDezenas, vezesJogadas)
    		
    		pare
	
		}
	}
	 

	funcao gerarNumeros(inteiro vetor[], inteiro quantidadeDezenas, inteiro valorInicial, inteiro valorFinal){
	    para(inteiro i = 0; i< quantidadeDezenas; i++){
		    
		    inteiro numeroGerado =  u.sorteia(valorInicial, valorFinal)
		    
		    se(existe(vetor, numeroGerado, i)){
		        i--
		    }
		    senao{
		        vetor[i] = numeroGerado
		    }
		}
	    
	}
	
	funcao logico existe(inteiro numerosGerados[], inteiro numeroGerado, inteiro quantidadeGerada){
	    
	    para(inteiro j = 0; j< quantidadeGerada; j++){
		        se(numerosGerados[j] == numeroGerado){
		            retorne verdadeiro
		        }
		    }
		retorne falso
	}
	
	funcao gasto(real valorDezenas[], inteiro quantidadeDezenas, inteiro vezesJogadas){
	    escreva("No total você gastou R$", valorDezenas[quantidadeDezenas]* vezesJogadas, "\n")
	}
	
	funcao ordenar (inteiro vetor[], inteiro tamanho){
		para (inteiro i = 0; i < tamanho; i++){
			para (inteiro j = 0; j < tamanho-1; j++){
				se (vetor [j] > vetor[j+1]){
					troca (vetor, j, j+1)
				}
			}
		}
	}
	
	funcao troca (inteiro v[], inteiro a, inteiro b){
		inteiro c = v[a]
		
		v[a] = v[b]
		v[b] = c
	}
	
 	

}
